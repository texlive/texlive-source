## texk/lcdf-typetools/ac/lcdf-typetools.ac
## configure options for lcdf-typetools.ac
m4_define([kpse_lcdf_typetools_progs],
[cfftot1 mmafm mmpfb otfinfo otftotfm t1dotlessj t1lint t1rawafm t1reencode t1testpage ttftotype42])[]dnl
AC_FOREACH([Kpse_Prog], kpse_lcdf_typetools_progs,
           [AC_ARG_ENABLE(Kpse_Prog,
                          AS_HELP_STRING([--disable-]Kpse_Prog,
                                         [do not build the ]Kpse_Prog[ program],
                                         [28]))])
m4_define([kpse_otftotfm_auto_opts], [cfftot1 t1dotlessj updmap])[]dnl
AC_FOREACH([Kpse_Opt], kpse_otftotfm_auto_opts,
           [AC_ARG_ENABLE(Kpse_Opt,
                          AS_HELP_STRING([--disable-auto-]Kpse_Opt,
                                         [disable running ]Kpse_Opt[ from otftotfm],
                                         [28]))])
