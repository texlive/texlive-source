dnl Check if we want to compile against an installed kpathsea library

EXTRA_LIBKPATHSEA_INCLUDE=
EXTRA_LIBKPATHSEA_LIBDIR=

if test "$with_system_kpathsea" = yes; then
  if test "x$with_kpathsea_libdir" != x; then
    EXTRA_LIBKPATHSEA_LIBDIR="-L$with_kpathsea_libdir"
  fi
  if test "x$with_kpathsea_include" != x; then
    EXTRA_LIBKPATHSEA_INCLUDE="-I$with_kpathsea_include"
  fi
fi

if test "$with_system_kpathsea" = yes; then
  OLD_LDFLAGS=$LDFLAGS
  OLD_CPP=$CPP
  OLD_CPPFLAGS=$CPPFLAGS
  OLD_LIBS=$LIBS
  
  LDFLAGS="$LDFLAGS $EXTRA_LIBKPATHSEA_LIBDIR"
  CPP="$CPP $EXTRA_LIBKPATHSEA_INCLUDE"
  CPPFLAGS="$EXTRA_LIBKPATHSEA_INCLUDE"

  AC_MSG_CHECKING(whether kpathsea defines kpse_enc_format)
  AC_TRY_COMPILE([#include "kpathsea/kpathsea.h"
  ], [kpse_file_format_type mytype = kpse_enc_format;
  ], have_good_kpathsea=yes, have_good_kpathsea=no)
  if test $have_good_kpathsea = no; then
    AC_MSG_RESULT(no)
    AC_MSG_WARN(kpathsea library is not recent enough; will compile my own)
    with_system_kpathsea=no
  else
    AC_MSG_RESULT(yes)
  fi

  LDFLAGS=$OLD_LDFLAGS
  CPP=$OLD_CPP
  CPPFLAGS=$OLD_CPPFLAGS
  LIBS=$OLD_LIBS
fi

if test "$with_system_kpathsea" = yes; then
  LIBKPATHSEACPPFLAGS="$EXTRA_LIBKPATHSEA_INCLUDE"
  LDLIBKPATHSEA="$EXTRA_LIBKPATHSEA_LIBDIR -lkpathsea"
  LIBKPATHSEADEP=
  using_system_kpathsea=yes
else
  LIBKPATHSEACPPFLAGS='-I$(kpathsea_dir)'
  LDLIBKPATHSEA='$(kpathsea_dir)/libkpathsea.la'
  LIBKPATHSEADEP='$(kpathsea_dir)/libkpathsea.la'
  using_system_kpathsea=no
fi


AC_SUBST(LIBKPATHSEACPPFLAGS)
AC_SUBST(LDLIBKPATHSEA)
AC_SUBST(LIBKPATHSEADEP)
