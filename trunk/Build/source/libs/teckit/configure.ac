dnl Process this file with autoconf to produce a configure script.
dnl
dnl   Copyright (C) 2009 Peter Breitenlohner <tex-live@tug.org>
dnl
dnl   This file is free software; the copyright holder
dnl   gives unlimited permission to copy and/or distribute it,
dnl   with or without modifications, as long as this notice is preserved.
dnl
AC_INIT([teckit for TeX Live], [2.5.1], [tex-k@tug.org])
AC_PREREQ([2.63])
AC_CONFIG_SRCDIR([source/UnicodeNames.cpp])
AC_CONFIG_AUX_DIR([../../build-aux])
AC_CONFIG_MACRO_DIR([../../m4])

AM_INIT_AUTOMAKE([foreign dist-bzip2])
AM_MAINTAINER_MODE

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_RANLIB
AC_PROG_LN_S

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h])

AC_C_BIGENDIAN

AC_CONFIG_HEADERS([config.h])

KPSE_ZLIB_FLAGS

AM_CONDITIONAL([build], [test "x$enable_build" != xno])

# Configure must not fail, or 'make dist' will fail as well; thus
# don't run any tests for '--disable-build', there may be no zlib.
if test "x$enable_build" != xno; then

save_CPPFLAGS=$CPPFLAGS
save_LIBS=$LIBS

CPPFLAGS="$CPPFLAGS $ZLIB_INCLUDES"
LIBS="$ZLIB_LIBS $LIBS"

AC_CHECK_FUNC([compress], , [AC_ERROR([zlib not found])])
AC_CHECK_HEADERS([zlib.h], , [AC_ERROR([zlib not found])])
AC_DEFINE([HAVE_LIBZ], 1, [Define if you have zlib.])

CPPFLAGS=$save_CPPFLAGS
LIBS=$save_LIBS

fi

AC_CONFIG_FILES([Makefile])

AC_OUTPUT
